FUNCTION "cylPosToAngle" : Real
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      cylPosRel : Real;
      Xp : Real;
      Yp : Real;
      Xv : Real;
      Yv : Real;
   END_VAR

   VAR_TEMP 
      Rv : Real;
      D0 : Real;
      phiV0 : Real;
      X1 : Real;
      X2 : Real;
      X3 : Real;
      SIN_PHI : Real;
      COS_PHI : Real;
   END_VAR


BEGIN
	(* Calcul de la position angulaire de la manoeuvre à partir de la position du vérin *)
	#Rv := SQRT((#Xv ** 2 + #Yv ** 2));
	#D0 := SQRT(((#Xp - #Xv) ** 2 + (#Yp - #Yv) ** 2));
	#phiV0 := ATAN(#Yv / #Xv);
	#X1 := 2 * #Rv * #Xp;
	#X2 := 2 * #Rv * #Yp;
	#X3 := #Xp ** 2 + #Yp ** 2 + #Rv ** 2 - (#D0 + #cylPosRel) ** 2;
	
	#SIN_PHI := (#X2 * #X3 + #X1 * SQRT((#X1 ** 2 + #X2 ** 2 - #X3 ** 2))) / (#X1 ** 2 + #X2 ** 2);
	#COS_PHI := (#X1 * #X3 - #X2 * SQRT((#X1 ** 2 + #X2 ** 2 - #X3 ** 2))) / (#X1 ** 2 + #X2 ** 2);
	
	#cylPosToAngle := "Atan2"("x" := #COS_PHI, "y" := #SIN_PHI) - #phiV0;
END_FUNCTION

